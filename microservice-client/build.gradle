buildscript {
    ext {
        springBootVersion = '1.3.3.RELEASE'
        appName = 'microservice-client'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("com.bmuschko:gradle-docker-plugin:2.6.8")
    }
}

apply plugin: 'spring-boot'
apply plugin: 'com.bmuschko.docker-remote-api'

dependencies {
    compile('org.springframework.boot:spring-boot-starter-velocity')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Brixton.M5"
    }
}

jar {
    baseName = appName
}

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

docker {
    url = 'https://192.168.99.100:2376'
    certPath = new File(System.properties['user.home'], '.docker/machine/machines/default')
}

task createDockerfile(type: Dockerfile) {
    dependsOn jar
    destFile = project.file('build/libs/Dockerfile')
    from 'daocloud.io/java:8'
    maintainer 'caihongwei "caihongwei2008@gmail.com"'
    volume '/tmp'
    exposePort 8080
    addFile appName + '-' + version + '.jar','app.jar'
    runCommand 'bash -c "touch /app.jar"'
    instruction 'ENTRYPOINT ["java","-jar","/app.jar"]'
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = createDockerfile.destFile.parentFile
    tag = 'caihongwei/' + appName + ':' + version
}