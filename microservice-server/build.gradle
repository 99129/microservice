buildscript {
    ext {
        springBootVersion = '1.3.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'spring-boot'

dependencies {
    compile('org.springframework.cloud:spring-cloud-starter-config')
    compile('org.springframework.cloud:spring-cloud-starter-eureka')
    compile('org.springframework.cloud:spring-cloud-starter-zuul')
    compile('org.springframework.cloud:spring-cloud-starter-bus-amqp')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-log4j2')

    compile('org.mybatis.spring.boot:mybatis-spring-boot-starter:' + mybatisVersion)
    runtime('mysql:mysql-connector-java:' + mysqlVersion)
    compile('com.alibaba:druid:' + druidVersion)
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

configurations {
    all*.exclude module: 'spring-boot-starter-logging'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Brixton.M5"
    }
}

ext {
    appName = 'server'
}

sourceSets {
    main {
        resources.srcDirs = ['src/main/resources', 'src/main/java']
        resources.includes = ['**/*.xml', '**/*.yml']
    }
}

jar {
    baseName = appName
}

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

task createDockerfile(type: Dockerfile) {
    dependsOn build
    destFile = project.file('build/libs/Dockerfile')
    from 'daocloud.io/java:8'
    maintainer 'caihongwei "caihongwei2008@gmail.com"'
    volume '/tmp'
    addFile appName + '-' + version + '.jar','app.jar'
    runCommand 'bash -c "touch /app.jar"'
    instruction 'ENTRYPOINT ["java","-jar","/app.jar"]'
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = createDockerfile.destFile.parentFile
    tag = 'microservice/' + appName + ':' + version
}
